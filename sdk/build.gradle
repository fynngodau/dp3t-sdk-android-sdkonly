/*
 * Created by Ubique Innovation AG
 * https://www.ubique.ch
 * Copyright (c) 2020. All rights reserved.
 */

plugins {
	id 'com.android.library'
	id 'maven-publish'
	id 'com.jfrog.bintray'
}

android {
	compileSdkVersion 29

	defaultConfig {
		minSdkVersion 21
		targetSdkVersion 29
		versionCode 2
		versionName "0.1.2"
		testInstrumentationRunnerArgument 'androidx.benchmark.suppressErrors', 'EMULATOR,LOW-BATTERY,ACTIVITY-MISSING,DEBUGGABLE,UNLOCKED,UNSUSTAINED-ACTIVITY-MISSING'
		testInstrumentationRunner "androidx.benchmark.junit4.AndroidBenchmarkRunner"

		consumerProguardFiles 'consumer-rules.pro'

		buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"
	}

	defaultPublishConfig "productionRelease"
	publishNonDefault true

	flavorDimensions "version"

	productFlavors {
		calibration {
		}
		production {
		}
	}

	buildTypes {
		release {
			minifyEnabled false
		}
	}

	compileOptions {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}
}

task androidSourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
	publishing {
		publications {
			"sdkRelease"(MavenPublication) {
				from components.productionRelease
				groupId 'org.dpppt'
				artifactId 'dp3t-sdk-android'
				version android.defaultConfig.versionName
				artifact androidSourcesJar
			}
		}
	}
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
}
bintray {
	user = properties.getProperty("bintray.user")
	key = properties.getProperty("bintray.apikey")
	publications = ['sdkRelease']
	publish = true
	pkg {
		repo = 'dp3t-sdk-android'
		name = 'dp3t-sdk-android'
		userOrg = 'dp3t'
		licenses = ['MPL-2.0']
	}
}

repositories {
	maven { url 'https://jitpack.io' }
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	implementation 'androidx.core:core:1.2.0'
	implementation 'androidx.work:work-runtime:2.3.4'

	implementation 'com.squareup.retrofit2:retrofit:2.6.2'
	implementation 'com.squareup.retrofit2:converter-gson:2.6.2'

	implementation 'com.github.BottleRocketStudios:Android-Vault:1.4.2'

	testImplementation 'junit:junit:4.12'

	androidTestImplementation "androidx.benchmark:benchmark-junit4:1.0.0"
	androidTestImplementation 'androidx.test.ext:junit:1.1.1'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
